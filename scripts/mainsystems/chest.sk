on load:
	broadcast "&7[&6*&7] &aSuccessfully registered drop inventory system."
on unload:
	broadcast "&7[&6*&7] &cUnregistered drop inventory system."

on pickup:
	set {_parameters} to tag "Parameters" of nbt of event-item
	event is not canceled
	if tag "Type" of {_parameters} is "アイテム" or "素材":
		if {player.dropinv::%player's uuid%} has enough space for event-item:
			cancel event
			set {_v} to vector between event-entity and player
			set event-entity's velocity to {_v}
			set metadata value "cancel" of event-entity to true
			play "ENTITY_ITEM_PICKUP" to player at volume 1
			add event-item to {player.dropinv::%player's uuid%}
			send "&a+:%event-item's name%&7[x%item amount of event-item%&7]" to player
			kill event-entity
on inventory click:
	if event-inventory's type is crafting table inventory:
		wait a tick
		if event-item is ender chest:	
			if event-item's name is "&a&lドロップ品ポーチ":
				if "%click type%" is "right mouse button with shift":
					set slot 4 of player's current inventory to chest named "&e&lドロップ品ポーチ"
					set {_lore::1} to "&f&l右クリックで&a&l確定"
					set {_lore::2} to "&7左クリックで&cキャンセル"
					set slot 4 of player's current inventory's lore to {_lore::*}
				else:
					open {player.dropinv::%player's uuid%} for player
		if event-item is chest:
			if event-item's name is "&e&lドロップ品ポーチ":
				if "%click type%" is "left mouse button":
					set slot 4 of player's current inventory to ender chest named "&a&lドロップ品ポーチ" with lore "&7シフト＋右クリックですべて売却"
				else if "%click type%" is "right mouse button":
					set {_c} to 0
					set {_total} to 0
					loop 27 times:
						if slot {_c} of {player.dropinv::%player's uuid%} is not air:
							set {_parameters} to tag "Parameters" of nbt of slot {_c} of {player.dropinv::%player's uuid%}
							set {_amount} to item amount of slot {_c} of {player.dropinv::%player's uuid%}
							set {_gold} to tag "Gold" of {_parameters}
							set {_gold} to {_gold} * {_amount}
							add {_gold} to {_total}
							set slot {_c} of {player.dropinv::%player's uuid%} to air
						delete {_parameters} and {_gold} and {_amount}
						add 1 to {_c}
					add {_total} to {player.gold::%player's uuid%}
					play "ENTITY_EXPERIENCE_ORB_PICKUP" to player at volume 1
					send "&6+: &e%{_total}% &eゴールド"
					refsb(player)
					set slot 4 of player's current inventory to ender chest named "&a&lドロップ品ポーチ" with lore "&7シフト＋右クリックですべて売却"
on quit:
	set {_inv} to {player.dropinv::%player's uuid%}
	set {_uuid} to uuid of player
	set {_inv::*} to saveInventory(player, {_inv})
	if file "../PlayerData/%{_uuid}%/dropinv.yml" does not exist:
		create file "../PlayerData/%{_uuid}%/dropinv.yml"
	set file contents of "../PlayerData/%{_uuid}%/dropinv.yml" to {_inv::*}
	delete {player.dropinv::%player's uuid%}

on join:
	set {_uuid} to uuid of player
	set {player.dropinv::%player's uuid%} to chest inventory with 3 rows named "&a&lドロップ品ポーチ"
	if file "../PlayerData/%{_uuid}%/dropinv.yml" exists:
		set {_inv::*} to file contents of "../PlayerData/%{_uuid}%/dropinv.yml"
		loop {_inv::*}:
			set {_cnt} to loop-index parsed as number
			set {_cnt} to {_cnt} - 1
			set {_itemdata::*} to loop-value split at "//"
			set {_itemdata::2} to {_itemdata::2} parsed as number
			if {_itemdata::3} is set:
				set {_item} to {_itemdata::2} of {item.save.list::%{_itemdata::1}%}
				add "{Stones:%{_itemdata::3}%}" to nbt of {_item}
				set slot {_cnt} of {player.dropinv::%player's uuid%} to refreshitem({_item})
			else:
				set slot {_cnt} of {player.dropinv::%player's uuid%} to {_itemdata::2} of {item.save.list::%{_itemdata::1}%}
			delete {_item} and {_itemdata::*}