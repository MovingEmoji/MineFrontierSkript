options:
	skill_name: "アクセラレーション"
	skill_type: "共通"
	mp: 0
	cool_time: 20 seconds

on load:
	registerSkill({@skill_name},{@skill_type})

function ForceAcceleration(p: player, type: text):
	set {_uuid} to uuid of {_p}
	if {player.%{_type}%.skill::%{_uuid}%} is {@skill_name}:
		if difference between {player.%{_type}%.cooldown::%{_uuid}%} and now >= {@cool_time}:
			if {@skill_type} contains {player.job::%{_uuid}%}:
				set {_t} to true
			else if {@skill_type} is "共通":
				set {_t} to true
			if {_t} is set:
				if {player.nowmp::%{_uuid}%} >= {@mp}:
					if {acceleration.%{_uuid}%} is not true:
						subtract {@mp} from {player.nowmp::%{_uuid}%}
						set {player.%{_type}%.cooldown::%{_uuid}%} to now
						set {acceleration.%{_uuid}%} to true
						playsound("BLOCK_BEACON_ACTIVATE",location of {_p},2,1)
						playsound("ENTITY_FIREWORK_ROCKET_LAUNCH",location of {_p},0.8,1)
						particle(5,"end_rod",0.3,1,0.3,0.1,location of {_p})
						particle(3,"SWEEP_ATTACK",0.3,0.4,0.3,0.1,location of {_p})
						set {_count} to 0
						while {acceleration.%{_uuid}%} is true:
							apply SPEED 2 without any particles to {_p} for 5 ticks
							wait 5 ticks
							add 1 to {_count}
							if {_count} == 4:
								set {_count} to 0
								particle(5,"end_rod",0.3,1,0.3,0.1,location of {_p})
								particle(3,"SWEEP_ATTACK",0.3,0.4,0.3,0.1,location of {_p})
						playsound("BLOCK_REDSTONE_TORCH_BURNOUT",location of {_p},1.5,1)
						playsound("BLOCK_BEACON_DEACTIVATE",location of {_p},1.5,1)
						remove SPEED from {_p}
					else:
						delete {acceleration.%{_uuid}%}
				else:
					send "&c&lMPが足りません..." to {_p}
			else:
				send "&c&l現在の職業ではこのスキルを使用できません" to {_p}
		else:
			if {acceleration.%{_uuid}%} is true:
				delete {acceleration.%{_uuid}%}
				set {player.mobile.cooldown::%{_uuid}%} to {@cool_time} seconds before now
			else:
				set {_d} to difference between {player.%{_type}%.cooldown::%{_uuid}%} and now
				set {player.skill.cooldown::%{_uuid}%} to 10
				play "BLOCK_NOTE_BLOCK_BASS" to {_p} at volume 2

on offhand switch:
	ForceAcceleration(player,"mobile")

on right click:
	if player is sneaking:
		ForceAcceleration(player,"common1")
on drop:
	if player is sneaking:
		ForceAcceleration(player,"common2")
	else:
		ForceAcceleration(player,"common3")

on damage:
	if victim is player:
		if {acceleration.%victim's uuid%} is true:
			delete {acceleration.%victim's uuid%}
	else:
		if attacker is player:
			if {acceleration.%attacker's uuid%} is true:
				delete {acceleration.%attacker's uuid%}

on quit:
	delete {acceleration.%event-player's uuid%}