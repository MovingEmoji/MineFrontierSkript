options:
	skill_name: "ブリンク"
	skill_type: "共通"
	mp: 0
	cool_time: 0.5 seconds

on load:
	registerSkill({@skill_name},{@skill_type})



#OWのトレーサーみたいにチャージ制にしても面白そう
function ForceBlink(p: player, type: text):
	set {_uuid} to uuid of {_p}
	if {player.%{_type}%.skill::%{_uuid}%} is {@skill_name}:
		if difference between {player.%{_type}%.cooldown::%{_uuid}%} and now >= {@cool_time}:
			if {@skill_type} contains {player.job::%{_uuid}%}:
				set {_t} to true
			else if {@skill_type} is "共通":
				set {_t} to true
			if {_t} is set:
				if {player.nowmp::%{_uuid}%} >= {@mp}:
					subtract {@mp} from {player.nowmp::%{_uuid}%}
					set {player.%{_type}%.cooldown::%{_uuid}%} to now
					set {_vector} to vector from yaw {_p}'s yaw and pitch -15
					set x of {_vector} to x of {_vector} * 1.3
					set z of {_vector} to z of {_vector} * 1.3
					set {_p}'s velocity to {_vector}
					particle(10,"CLOUD",1,0,1,0.1,location of {_p})
					playsound("ENTITY_FIREWORK_ROCKET_LAUNCH",location of {_p},2,0.5)
					playsound("ENTITY_BLAZE_SHOOT",location of {_p},0.5,0.5)
					playsound("ENTITY_ENDER_DRAGON_FLAP",location of {_p},0.9,1)
					wait 2 tick
					playsound("ENTITY_ENDER_DRAGON_FLAP",location of {_p},1.2,1)
					wait 2 tick
					playsound("ENTITY_ENDER_DRAGON_FLAP",location of {_p},1.5,1)

				else:
					send "&c&lMPが足りません..." to {_p}
			else:
				send "&c&l現在の職業ではこのスキルを使用できません" to {_p}
		else:
			set {_d} to difference between {player.%{_type}%.cooldown::%{_uuid}%} and now
			set {player.skill.cooldown::%{_uuid}%} to 10
			play "BLOCK_NOTE_BLOCK_BASS" to {_p} at volume 2

on offhand switch:
	ForceBlink(player,"mobile")

on right click:
	if player is sneaking:
		ForceBlink(player,"common1")
on drop:
	if player is sneaking:
		ForceBlink(player,"common2")
	else:
		ForceBlink(player,"common3")