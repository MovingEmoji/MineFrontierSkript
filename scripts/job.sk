on load:
	delete {list.profile::*}
	add "level" to {list.profile::*}
	add "xp" to {list.profile::*}
	add "needxp" to {list.profile::*}
	add "damage" to {list.profile::*}
	add "protection" to {list.profile::*}
	add "magicdamage" to {list.profile::*}
	add "mp" to {list.profile::*}
	add "health" to {list.profile::*}
	add "dex" to {list.profile::*}

	delete {list.upstatus::*}
	add "damage" to {list.upstatus::*}
	add "protection" to {list.upstatus::*}
	add "magicdamage" to {list.upstatus::*}
	add "mp" to {list.upstatus::*}
	add "health" to {list.upstatus::*}
	add "dex" to {list.upstatus::*}

function loadprofile(p: player, t: text):
	set {_uuid} to uuid of {_p}
	if {_t} is "lastjob":
		if file "../PlayerData/%{_uuid}%/lastjob.yml" exists:
			set {_job} to line 1 in file "../PlayerData/%{_uuid}%/lastjob.yml"
			set {_contents::*} to file contents of "../PlayerData/%{_uuid}%/%{_job}%.yml"
			set {player.job::%{_uuid}%} to {_job}
			loop {list.profile::*}:
				set {_content} to {_contents::%loop-index%}
				replace "%loop-value%: " with "" in {_content}
				set {player.%loop-value%::%{_uuid}%} to {_content} parsed as number
			set {_load} to true
		else:
			execute console command "/nte player %{_p}% suffix  &6[ビギナー&6] &7[&e×&7]"
	else:
		if file "../PlayerData/%{_uuid}%/%{_t}%.yml" exists:
			set {_contents::*} to file contents of "../PlayerData/%{_uuid}%/%{_t}%.yml"
			set {player.job::%{_uuid}%} to {_t}
			loop {list.profile::*}:
				set {_content} to {_contents::%loop-index%}
				replace "%loop-value%: " with "" in {_content}
				set {player.%loop-value%::%{_uuid}%} to {_content} parsed as number
		else:
			set {player.job::%{_uuid}%} to {_t}
			set {_contents::*} to file contents of "../BaseProfile/profile.yml"
			loop {list.profile::*}:
				set {_content} to {_contents::%loop-index%}
				replace "%loop-value%: " with "" in {_content}
				set {player.%loop-value%::%{_uuid}%} to {_content} parsed as number
		if file "../PlayerData/%{_uuid}%/lastjob.yml" exists:
			set line 1 in file "../PlayerData/%{_uuid}%/lastjob.yml" to {_t}
		else:
			create file "../PlayerData/%{_uuid}%/lastjob.yml"
			write {_t} at line 1 to file "../PlayerData/%{_uuid}%/lastjob.yml"
		set {_job} to {_t}
		set {_load} to true
	if {_load} is true:
		send "&a%{_job}%&aのデータを読み込みました" to {_p}
		set {player.nowmp::%{_uuid}%} to 0
		set {player.nowhealth::%{_uuid}%} to {player.health::%{_uuid}%}
		set {_p}'s level to {player.level::%{_uuid}%}
		set {_p}'s level progress to {player.xp::%{_uuid}%} / {player.needxp::%{_uuid}%} + 0.01
		execute console command "/nte player %{_p}% suffix  &6[%{player.job::%{_uuid}%}%&6] &7[&e%{player.level::%{_uuid}%}%&7]"
		refsb({_p})

function saveprofile(p: player):
	set {_uuid} to uuid of {_p}
	if {player.job::%{_uuid}%} is set:
		if file "../PlayerData/%{_uuid}%/%{player.job::%{_uuid}%}%.yml" does not exist:
			create file "../PlayerData/%{_uuid}%/%{player.job::%{_uuid}%}%.yml"
			loop {list.profile::*}:
				set {_index} to loop-index parsed as number
				write "%loop-value%: %{player.%loop-value%::%{_uuid}%}%" at line {_index} to file "../PlayerData/%{_uuid}%/%{player.job::%{_uuid}%}%.yml"
				delete {player.%loop-value%::%{_uuid}%}
		else:
			loop {list.profile::*}:
				set {_index} to loop-index parsed as number
				set line {_index} in file "../PlayerData/%{_uuid}%/%{player.job::%{_uuid}%}%.yml" to "%loop-value%: %{player.%loop-value%::%{_uuid}%}%"
				delete {player.%loop-value%::%{_uuid}%}
		delete {player.job::%{_uuid}%}


function leveltable(p: player):
	set {_uuid} to uuid of {_p}
	while {player.needxp::%{_uuid}%} <= {player.xp::%{_uuid}%}:
		add 1 to {player.level::%{_uuid}%}
		subtract {player.needxp::%{_uuid}%} from {player.xp::%{_uuid}%}
		set {player.needxp::%{_uuid}%} to rounded up {player.needxp::%{_uuid}%} * 1.1
		set {_levelup} to true
		loop {list.upstatus::*}:
			add 1 to {player.%loop-value%::%{_uuid}%}
			if loop-value is "health":
				add {player.level::%{_uuid}%} - 1 to {player.%loop-value%::%{_uuid}%}
			else:
				set {_n} to rounded down {player.level::%{_uuid}%} / 10
				add {_n} to {player.%loop-value%::%{_uuid}%}

	if {_levelup} is set:
		play "ENTITY_PLAYER_LEVELUP" to {_p} at volume 1
		send "&6&lLEVEL UP! (CurrentLevel: %{player.level::%{_uuid}%}%&6)" to {_p}
		execute console command "/nte player %{_p}% suffix  &6[%{player.job::%{_uuid}%}%&6] &7[&e%{player.level::%{_uuid}%}%&7]"
	set {_p}'s level to {player.level::%{_uuid}%}
	set {_p}'s level progress to {player.xp::%{_uuid}%} / {player.needxp::%{_uuid}%} + 0.01
function givexp(p: player, n: number):
	play "ENTITY_EXPERIENCE_ORB_PICKUP" to {_p} at volume 1
	set {_uuid} to uuid of {_p}
	set {player.xp::%{_uuid}%} to {player.xp::%{_uuid}%} + {_n}
	leveltable({_p})
	wait 5 tick
	refsb({_p})

function setxp(p: player, n: number):
	set {_uuid} to uuid of {_p}
	set {player.xp::%{_uuid}%} to {_n}
	leveltable({_p})
	wait 5 tick
	refsb({_p})

command /xp [<text>] [<number>]:
	permission: admin
	trigger:
		if arg-1 is "give":
			send "&a%arg-2%&aXPを獲得しました"
			givexp(player,arg-2)
			stop
		if arg-1 is "set":
			send "&a%arg-2%&aXPに設定しました"
			setxp(player,arg-2)
			stop
command /job [<text>] [<text>]:
	permission: admin
	trigger:
		if arg-1 is set:
			if arg-1 is "create":
				if arg-2 is set:
					loop {save.job.list::*}:
						if arg-2 is loop-value:
							set {_stop} to true
					if {_stop} is set:
						send "&cその職業は既に存在しています"
					else:
						send "&a職業 &7[&6%arg-2%&7] &aを作成しました"
						add arg-2 to {save.job.list::*}
				else:
					send "&c作成する職業名を入力して下さい"
			if arg-1 is "reset":
				send "&aステータスをリセットしました"
				delete file "../PlayerData/%player's uuid%/%{player.job::%player's uuid%}%.yml"
				loadprofile(player,{player.job::%player's uuid%})
			if arg-1 is "list":
				if {save.job.list::*} is set:
					send "&7-&a現在の職業リスト&7-"
					loop {save.job.list::*}:
						send "&7・&6%loop-value%"
				else:
					send "&c職業が存在しません"
			if arg-1 is "delete":
				if arg-2 is set:
					loop {save.job.list::*}:
						if arg-2 is loop-value:
							delete {save.job.list::%loop-index%}
							send "&c職業 &7[&6%arg-2%&7] &cを削除しました"
							stop
					send "&c指定された職業は見つかりませんでした"
				else:
					send "&c削除する職業名を入力してください"
		else:
			send "&c実行内容を入力してください"
command /setjob [<text>]:
	permission: admin
	trigger:
		if arg-1 is set:
			loop {save.job.list::*}:
				if loop-value is arg-1:
					set {_stop} to true
					saveprofile(player)
					loadprofile(player, arg-1)
			if {_stop} is not set:
				send "&cその職業は見つかりませんでした"
		else:
			send "&c職業名を入力してください！"
on click:
	if event-block is enchanting table:
		if block 1 below location of event-block is diamond block:
			cancel event
			wait a tick
			heal player
			set {player.nowmp::%player's uuid%} to {player.mp::%player's uuid%} + {player.item.mp::%player's uuid%}
			set {player.location::%player's uuid%} to location of player
			teleport player to {save.location::jobchange}
			hide all players from player
			send "&a&l設定したい項目を選択してください"
			
on click:
	if player's target is armor stand:
		if distance between player and player's target < 3:
			set {_t} to player's target
			if player's target's display name is "&c&l戻る":
				cancel event
				if {player.inventory::%player's uuid%} is not set:
					teleport player to {player.location::%player's uuid%}
					delete {player.location::%player's uuid%}
					reveal all players from player
					send "&7テレポート中..."
			if player's target's display name is "&a&l職業設定":
				cancel event
				if {player.inventory::%player's uuid%} is not set:
					set {player.inventory::%player's uuid%} to base 64 from inventory player's inventory
					set {player.helmet::%player's uuid%} to player's helmet
					set {player.chestplate::%player's uuid%} to player's chestplate
					set {player.leggings::%player's uuid%} to player's leggings
					set {player.boots::%player's uuid%} to player's boots
					if {player.job::%player's uuid%} is not set:
						teleport player to {save.location::jobsensi}
					if {player.job::%player's uuid%} is "戦士":
						set player's selected hotbar slot to 4
						teleport player to {save.location::jobsensi}
						send title "&e&l戦士" with subtitle "&7攻撃力と防御力が高い万能な職業" to player for 2 seconds
					if {player.job::%player's uuid%} is "旅人":
						teleport player to {save.location::jobtabibito}
						set player's selected hotbar slot to 5
						send title "&e&l旅人" with subtitle "&7なんでも卒なくこなせる便利な職業" to player for 2 seconds
					if {player.job::%player's uuid%} is "アサシン":
						set player's selected hotbar slot to 6
						teleport player to {save.location::jobasasin}
						send title "&e&lアサシン" with subtitle "&7素早い攻撃と攻撃魔法が得意な職業" to player for 2 seconds
					if {player.job::%player's uuid%} is "メイジ":
						teleport player to {save.location::jobmeiji}
						set player's selected hotbar slot to 3
						send title "&e&lメイジ" with subtitle "&7魔法で遠距離火力を出しやすい職業" to player for 2 seconds
					if {player.job::%player's uuid%} is "プリースト":
						set player's selected hotbar slot to 2
						teleport player to {save.location::jobpuri-suto}
						send title "&e&lプリースト" with subtitle "&7魔法で自分やパーティメンバーを癒す職業" to player for 2 seconds
					clear player's inventory
					set slot 2 of player's inventory to golden carrot named "&a&lプリースト"
					set slot 3 of player's inventory to book named "&a&lメイジ"
					set slot 4 of player's inventory to iron sword named "&a&l戦士"
					set slot 5 of player's inventory to feather named "&a&l旅人"
					set slot 6 of player's inventory to shears named "&a&lアサシン"
					set slot 8 of player's inventory to red bed named "&c&l戻る"
on right click:
	if event-item is red bed named "&c&l戻る":
		cancel event
		teleport player to {player.location::%player's uuid%}
		delete {player.location::%player's uuid%}
		reveal all players from player
		send "&7テレポート中..."
		#clear player's inventory
		set player's inventory to inventory from base 64 {player.inventory::%player's uuid%}
		set  player's helmet to {player.helmet::%player's uuid%}
		set player's chestplate to {player.chestplate::%player's uuid%}
		set player's leggings to {player.leggings::%player's uuid%}
		set player's boots to  {player.boots::%player's uuid%}
		delete {player.inventory::%player's uuid%} and {player.helmet::%player's uuid%} and {player.chestplate::%player's uuid%} and {player.leggings::%player's uuid%} and {player.boots::%player's uuid%}
on quit:
	wait a tick
	saveprofile(player)
on join:
	loadprofile(player, "lastjob")

command /status:
	trigger:
		set {_uuid} to player's uuid
		set {_MeeleD} to {player.damage::%{_uuid}%} + {result.damage::%{_uuid}%}
		set {_HP} to {player.health::%{_uuid}%} + {result.health::%{_uuid}%}
		set {_PT} to {player.protection::%{_uuid}%} + {result.protection::%{_uuid}%}
		set {_MP} to {player.mp::%{_uuid}%} + {result.mp::%{_uuid}%}
		set {_MagicD} to {player.magicdamage::%{_uuid}%} + {result.magicdamage::%{_uuid}%}
		set {_dex} to {player.dex::%{_uuid}%} + {result.dex::%{_uuid}%}
		send "&7&m------------------------" to player
		send "&6&lXP: &f%{player.xp::%{_uuid}%}%" to player
		send "&6&lNeedXP: &f%{player.needxp::%{_uuid}%} - {player.xp::%{_uuid}%}%" to player
		send "&7&m------------------------" to player
		send "&c&l攻撃力: &f%{_MeeleD}%" to player
		send "&9&l防御力: &f%{_PT}%" to player
		send "&e&l魔力: &f%{_MagicD}%" to player
		send "&a&l体力: &f%{_HP}%" to player
		send "&d&lMP: &f%{_MP}%" to player
		send "&2&lDEX: &f%{_dex}%" to player
		send "&7&m------------------------" to player
on tool change:
	if {player.inventory::%player's uuid%} is set:
		wait a tick
		if player's tool's name is "&a&l戦士":
			teleport player to {save.location::jobsensi}
			send title "&e&l戦士" with subtitle "&7攻撃力と防御力が高い万能な職業" to player for 2 seconds
			play "ENTITY_PLAYER_LEVELUP" to player at volume 1
		if player's tool's name is "&a&l旅人":
			teleport player to {save.location::jobtabibito}
			send title "&e&l旅人" with subtitle "&7なんでも卒なくこなせる便利な職業" to player for 2 seconds
			play "ENTITY_PLAYER_LEVELUP" to player at volume 1
		if player's tool's name is "&a&lアサシン":
			teleport player to {save.location::jobasasin}
			send title "&e&lアサシン" with subtitle "&7素早い攻撃と攻撃魔法が得意な職業" to player for 2 seconds
			play "ENTITY_PLAYER_LEVELUP" to player at volume 1
		if player's tool's name is "&a&lメイジ":
			teleport player to {save.location::jobmeiji}
			send title "&e&lメイジ" with subtitle "&7魔法で遠距離火力を出しやすい職業" to player for 2 seconds
			play "ENTITY_PLAYER_LEVELUP" to player at volume 1
		if player's tool's name is "&a&lプリースト":
			teleport player to {save.location::jobpuri-suto}
			send title "&e&lプリースト" with subtitle "&7魔法で自分やパーティメンバーを癒す職業" to player for 2 seconds
			play "ENTITY_PLAYER_LEVELUP" to player at volume 1
on right click:
	if event-item is iron sword named "&a&l戦士":
		cancel event
		saveprofile(player)
		loadprofile(player, "戦士")
		set {_t} to true
	if event-item is feather named "&a&l旅人":
		cancel event
		saveprofile(player)
		loadprofile(player, "旅人")
		set {_t} to true
	if event-item is shears named "&a&lアサシン":
		cancel event
		saveprofile(player)
		loadprofile(player, "アサシン")
		set {_t} to true
	if event-item is book named "&a&lメイジ":
		cancel event
		saveprofile(player)
		loadprofile(player, "メイジ")
		set {_t} to true
	if event-item is golden carrot named "&a&lプリースト":
		cancel event
		saveprofile(player)
		loadprofile(player, "プリースト")
		set {_t} to true
	if {_t} is set:
		teleport player to {player.location::%player's uuid%}
		reveal all players from player
		send "&7テレポート中..."
		set player's inventory to inventory from base 64 {player.inventory::%player's uuid%}
		set  player's helmet to {player.helmet::%player's uuid%}
		set player's chestplate to {player.chestplate::%player's uuid%}
		set player's leggings to {player.leggings::%player's uuid%}
		set player's boots to  {player.boots::%player's uuid%}
		delete {player.inventory::%player's uuid%} and {player.helmet::%player's uuid%} and {player.chestplate::%player's uuid%} and {player.leggings::%player's uuid%} and {player.boots::%player's uuid%}
on quit:
	if {player.location::%player's uuid%} is set:
		teleport player to {player.location::%player's uuid%}
		if {player.inventory::%player's uuid%} is set:
			set player's inventory to inventory from base 64 {player.inventory::%player's uuid%}
			set  player's helmet to {player.helmet::%player's uuid%}
			set player's chestplate to {player.chestplate::%player's uuid%}
			set player's leggings to {player.leggings::%player's uuid%}
			set player's boots to  {player.boots::%player's uuid%}
	delete {player.inventory::%player's uuid%} and {player.helmet::%player's uuid%} and {player.chestplate::%player's uuid%} and {player.leggings::%player's uuid%} and {player.boots::%player's uuid%}
	delete {player.location::%player's uuid%}